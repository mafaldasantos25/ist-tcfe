*-----------------------------------------------------------------------------
*
* To use a subcircuit, the name must begin with 'X'.  For example:
* X1 1 2 3 4 5 uA741
*
* connections:   non-inverting input
*                |  inverting input
*                |  |  positive power supply
*                |  |  |  negative power supply
*                |  |  |  |  output
*                |  |  |  |  |
.subckt uA741    1  2  3  4  5
*
  c1   11 12 8.661E-12
  c2    6  7 30.00E-12
  dc    5 53 dx
  de   54  5 dx
  dlp  90 91 dx
  dln  92 90 dx
  dp    4  3 dx
  egnd 99  0 poly(2) (3,0) (4,0) 0 .5 .5
  fb    7 99 poly(5) vb vc ve vlp vln 0 10.61E6 -10E6 10E6 10E6 -10E6
  ga    6  0 11 12 188.5E-6
  gcm   0  6 10 99 5.961E-9
  iee  10  4 dc 15.16E-6
  hlim 90  0 vlim 1K
  q1   11  2 13 qx
  q2   12  1 14 qx
  r2    6  9 100.0E3
  rc1   3 11 5.305E3
  rc2   3 12 5.305E3
  re1  13 10 1.836E3
  re2  14 10 1.836E3
  ree  10 99 13.19E6
  ro1   8  5 50
  ro2   7 99 100
  rp    3  4 18.16E3
  vb    9  0 dc 0
  vc    3 53 dc 1
  ve   54  4 dc 1
  vlim  7  8 dc 0
  vlp  91  0 dc 40
  vln   0 92 dc 40
.model dx D(Is=800.0E-18 Rs=1)
.model qx NPN(Is=800.0E-18 Bf=93.75)
.ends



.options savecurrents

Vcc vcc 0 5.0
Vee vee 0 -5.0
Vin in 0 0 ac 1.0 sin(0 10m 1k)
Fteste dummy1 0 Vin 1
Rteste dummy1 0 10k


X1 ninv inv_in vcc vee out uA741


R1 ninv 0 1k
C1 in ninv 220n
R2 out out2 1k
C2 out2 d1 1u
C21 d1 d2 220n
C22 d2 0 220n
Ra d1 0 100k
Rb d2 0 100k
R3 inv_in 0 1k
R4 out inv_in 100k

.control


* time analysis
tran 1e-5 1e-2



* frequency analysis
ac dec 10 10 100MEG



let lower_bond = vecmax(vdb(out2))-3
meas ac f2 WHEN vdb(out2)=lower_bond CROSS=LAST
meas ac f1 WHEN vdb(out2)=lower_bond RISE=1


print f1
print f2
hardcopy vo1f.ps vdb(out2)
hardcopy vphase.ps vp(out2)*180/3.1415
echo vphase_FIG
echo vo1f_FIG
let centralfrequency = sqrt(f1*f2)
let gain2 = abs(vecmax(v(out2)))
let gain = abs(vecmax(vdb(out2)))
let inputimpedance = v(in)[20]/(v(dummy1)[20]/10000)
let merit = 1/((13323+314.66)*((sqrt(f1*f2)-1000)+(100-gain2)+0.000001))
echo "p_TAB"
*central frequency
print centralfrequency
print gain

*input impedance in ohm
print inputimpedance

*merit 
print merit

echo "p_END"
quit
.endc 
